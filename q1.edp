//---------------------------------------------------------------------//
//
//  Simulation of the linear Stokes equation in fix rectangle
//
//  Authors: Chongmo LIU
//           Tiago Lobato Gimenes
//
//---------------------------------------------------------------------//

//---------------------------------------------------------------------//
// General definitions for the whole program
//---------------------------------------------------------------------//

// Boundaries definitions
real x0=0, x1=0.1;
real y0=0, y1=0.006;

// Density of the mesh definitions
int n=30, m=40;

// Physical constants definitions
real nu=0.01;

//---------------------------------------------------------------------//
// Definitions for the problem without the transpose matrix
//---------------------------------------------------------------------//

// Mesh creation
mesh Th=square(n,m, [x0+(x1-x0)*x, y0+(y1-y0)*y]);

// Function spaces creation
fespace Xh(Th, P2);
fespace Mh(Th, P1);

// Definition of the problem's variable
Xh u2, v2;
Xh u1, v1;
Mh p,q;

// Solves the linear Stokes formula
problem Stokes(u1, u2, p, v1, v2, q, solver=Crout)=
    int2d(Th)(nu*(dx(u1)*dx(v1)+dy(u1)*dy(v1)+dx(u2)*dx(v2)+dy(u2)*dy(v2))
    -p*q*(0.000001)-p*dx(v1)-p*dy(v2)-dx(u1)*q-dy(u2)*q)
    +on(1, 3, u1=0,u2=0)
    +on(4, u1=-(y-y1/2)^2/(2*nu)+(y1/2)^2/(2*nu), u2=0);

//---------------------------------------------------------------------//
// Definitions for the problem with the transpose matrix
//---------------------------------------------------------------------//

// Mesh creation
mesh Th2=square(n,m, [x0+(x1-x0)*x, y0+(y1-y0)*y]);

// Function spaces creation
fespace Xh2(Th2, P2);
fespace Mh2(Th2, P1);

// Definition of the problem's variable
Xh2 u22, v22;
Xh2 u12, v12;
Mh2 p2,q2;

// Solves the linear Stokes formula
problem Stokes2(u12, u22, p2, v12, v22, q2, solver=Crout)=
    int2d(Th2)(nu*(dx(u12)*dx(v12)+dy(u12)*dy(v12)+dx(u22)*dx(v22)+dy(u22)*dy(v22)
    +dx(u12)*dx(v12)+dx(u22)*dy(v12)+dy(u12)*dx(v22)+dy(u22)*dy(v22))
    -p2*q2*(0.000001)-p2*dx(v12)-p2*dy(v22)-dx(u12)*q2-dy(u22)*q2)
    +on(1, 3, u12=0,u22=0)
    +on(4, u12=-(y-y1/2)^2/(2*nu)+(y1/2)^2/(2*nu), u22=0);

//---------------------------------------------------------------------//
// Execution of the program without the transpose matrix
//---------------------------------------------------------------------//

// Plots for the first time
Stokes; plot(p, [u1,u2]);

// Adapts the mesh to better calculation
Th = adaptmesh(Th, p, err=1.e-3);

// Plots the solution for the second time
Stokes; plot(p,[u1, u2]);

//---------------------------------------------------------------------//
// Execution of the program with the transpose matrix
//---------------------------------------------------------------------//

// Plots for the first time
Stokes2; plot(p2, [u12,u22]);

// Adapts the mesh to better calculation
Th2 = adaptmesh(Th2, p2, err=1.e-3);

// Plots the solution for the second time
Stokes2; plot(p2,[u12, u22]);
